apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-artifacts.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply from: "${rootDir}/gradle/maven-publishing.gradle"
apply plugin: 'org.kordamp.gradle.stats'

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.guing.RunPlantOverview'
}
mainClassName = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

dependencies {
  compile project(':openTCS-API-Injection')
  compile project(':openTCS-Common')
  compile project(':openTCS-Impl-Configuration-cfg4j')
  compile project(':openTCS-PlantOverview-Base')
  compile project(':openTCS-PlantOverview-Panel-LoadGenerator')
  compile project(':openTCS-PlantOverview-Panel-ResourceAllocation')
  compile project(':openTCS-PlantOverview-Panel-Statistics')
  compile project(':openTCS-PlantOverview-Panel-Video')
  compile project(':openTCS-PlantOverview-Themes-Default')
  
  // There does not seem to be an official binary release for JHotDraw...
  compile group: 'org.opentcs.thirdparty.jhotdraw', name: 'jhotdraw', version: '7.6.20190506'
  compileOnly group: 'org.opentcs.thirdparty.jhotdraw', name: 'jhotdraw', version: '7.6.20190506', classifier: 'sources'
  // This preview version of Docking Frames is not in Maven Central, yet.
  compile group: 'org.opentcs.thirdparty.dockingframes', name: 'docking-frames-common', version: '1.1.2p11'
  compileOnly group: 'org.opentcs.thirdparty.dockingframes', name: 'docking-frames-common', version: '1.1.2p11', classifier: 'sources'
  compile group: 'org.opentcs.thirdparty.dockingframes', name: 'docking-frames-core', version: '1.1.2p11'
  compileOnly group: 'org.opentcs.thirdparty.dockingframes', name: 'docking-frames-core', version: '1.1.2p11', classifier: 'sources'

  runtime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.21'

  compile files("${rootDir}/lib/swingexplorer-core-1.9.0.jar")
  compile files("${rootDir}/lib/swingexplorer-agent-1.9.0.jar")
  compile files("${rootDir}/lib/flatlaf-1.4.jar")
  compile files("${rootDir}/lib/flatlaf-extras-1.4.jar")
  implementation 'com.oliphantllc:swing-layout:1.0.3'
  implementation 'javassist:javassist:3.12.1.GA'
  implementation 'com.formdev:flatlaf-extras:1.4'
  implementation 'com.formdev:flatlaf:1.4'
  implementation 'com.formdev:flatlaf-intellij-themes:1.4'
  implementation 'com.jgoodies:jgoodies-forms:1.9.0'
  // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
  implementation group: 'com.miglayout', name: 'miglayout-swing', version: '5.3'

}

compileJava {
  options.compilerArgs << "-Xlint:-rawtypes"
}

distributions {
  main {
    contents {
      from "${sourceSets.main.resources.srcDirs[0]}/org/opentcs/plantoverview/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'java.security.policy':'file:./config/java.policy',\
                    'sun.java2d.d3d':'false',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.cfg4j.reload.interval':'10000'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow',\
          '-splash:bin/splash-image.gif')
}

task(runSimple, dependsOn: 'classes', type: JavaExec) {
  main = 'org.opentcs.guing.RunPlantOverview2'
  classpath = sourceSets.main.runtimeClasspath
  args 'mrhaki'
  systemProperties(['java.util.logging.config.file': './config/logging.config',        \
                            'java.security.policy': 'file:./config/java.policy',        \
                            'sun.java2d.d3d': 'false',        \
                            'opentcs.base': '.',        \
                            'opentcs.home': '.',        \
                            'opentcs.cfg4j.reload.interval': '10000'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow',        \
                  '-splash:bin/splash-image.gif')
}